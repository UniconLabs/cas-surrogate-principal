apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: "signing"

apply from: 'http://tellurianring.com/projects/gradle-plugins/gradle-release/apply.groovy'

repositories {
    jcenter()

    maven { url "https://raw.github.com/synergian/wagon-git/releases"}
}

configurations {
    provided
    deployerJar
}

dependencies {
    provided "org.jasig.cas:cas-server-core:${project."cas.version"}"

    testCompile "org.codehaus.groovy:groovy-all:${project."groovy.version"}"
    testCompile "org.spockframework:spock-core:${project."spock.version"}"

    deployerJar "ar.com.synergian:wagon-git:0.2.0"
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

idea {
    module {
        scopes.PROVIDED.plus += configurations.provided
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

createReleaseTag.dependsOn uploadArchives

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment)}
            configuration = configurations.deployerJar
            repository(url: "https://api.bintray.com/maven/uniconiam/maven/cas-surrogate-principal") {
                authentication(userName: bintrayUsername, password: bintrayAPIKey)
            }
            snapshotRepository(url: "git:snapshots://git@bitbucket.org:scalding/maven-repo.git") {
                authentication(userName: githubUsername, password: githubPassword)
            }
            pom.project {
                name "CAS Surrogate Principal"
                packaging "jar"
                description "An Apereo CAS authentication handler to allow surrogate authentication."
                url "https://github.com/UniconLabs/cas-surrogate-principal"

                scm {
                    url "scm:git@github.com/UniconLabs/cas-surrogate-principal"
                    connection "scm:git@github.com/UniconLabs/cas-surrogate-principal"
                    developerConnection "scm:git@github.com/UniconLabs/cas-surrogate-principal"
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'scalding'
                        name 'Jonathan Johnson'
                    }
                    developer {
                        id 'mglazier'
                        name 'Matt Glazier'
                    }
                }
            }
        }
    }
}